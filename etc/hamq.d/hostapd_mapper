#!/usr/bin/awk -f 
# 
# hostapd_mapper reads hostapd_tracker/# topics (via wrapper)
# and sets 
#

BEGIN { 
    #read our mac table from a conf file
    while ( (getline line < "/etc/hamq.d/hostapd_mapper.conf") > 0){
               #should do some checking/sanitation, at least allow for comments
               split(line, tmp,"=")
               mac=tmp[1]
               logical=tmp[2]
               mactolog[mac]=logical
          }
    close("/etc/hamq.d/hostapd_mapper.conf")
     
    #split topic on / and _space_ for msg
    #not very safe (topics with spaces etc)
    #but good enough for now
      FS = "/| " 

    # only handle these commands from hostapd
	#  
	hostapd_tracker_cmds["AP-STA-CONNECTED"]=1
	hostapd_tracker_cmds["AP-STA-DISCONNECTED"]=1
	#
	#
}



#handle hostapd_tracker events
$1=="hostapd_tracker" && ($5 in hostapd_tracker_cmds){ 
#
# expected format
# hostapd_tracker/device/iface/mac AP-STA-CONNECTED
# hostapd_tracker/device/iface/mac AP-STA-DISCONNECTED
#
 device = $2
 iface = $3
 deviceiface = $2$3
 mac = $4
 cmd = $5
#
 
# store event per device_interface combo (assumed to be unique)
# multiple instances device/iface combos can report the same mac
# store events separately and OR them to avoid a race condition
# when different APs would report DISCONNECTED/CONNECTED out of order
 if (cmd=="AP-STA-CONNECTED"){
   state[mac,deviceiface]=1 #TRUE
 }
 if (cmd=="AP-STA-DISCONNECTED"){
   state[mac,deviceiface]="" #FALSE
 }

# iterate over all interfaces for a given mac
# OR all device_interface states
# mentalnote: doublecheck how AWK OR's
 macstate="" #FALSE
 for (key in state){
   split( key, i, SUBSEP )
   if (i[1]==mac){ 
     macstate=macstate || state[mac,i[2]]
   }
 }

#map mac to logical
 logical=mactolog[mac] 
 if (!logical){
	logical="UKNOWN"
	mactolog[mac]=logical
    #update our conf file for easy adding of new devices
    print mac "=" logical >> "/etc/hamq.d/hostapd_mapper.conf"
 }

# update the power state for the logical device
# we are using CONNECTED as a proxy for powered
# !we are not handling anything but connect/disc sofar so this should be safe
 if (macstate) { system("source /etc/hamq.d/hamq.conf && mosquitto_pub -h $HOST -p $PORT -u $USER -P $PASS -I hostapd_mapper -t " logical "/power -m ON") }
 else {	system("source /etc/hamq.d/hamq.conf && mosquitto_pub -h $HOST -p $PORT -u $USER -P $PASS -I hostapd_mapper  -t " logical "/power -m OFF") }

} ## end of hostapd_tracker


